// Looks back for last updates of DeviceInfo and compares it to last entries in MDI Data 
// If there where AD SignIns in the last couple of days, but not updates to DeviceInfo, records will be shown
// To detect possible disablement of defender for endpoint 
// @janvonkirchheim
//
let lookback = timespan(30d); //how log do you want to look back for DeviceInfo and AADSignIns
let timeDiff = timespan(4d); //how many days between last seen by Defender and last seen AAD SignIn
let OnbardedDevices = DeviceInfo 
| where OnboardingStatus == "Onboarded"
| summarize T1 = arg_max(Timestamp, *) by DeviceName
| where T1 > ago(lookback)
| extend  hostname1 = extract("[^.]*",0, tolower(DeviceName)) 
| project D1 = tolower(hostname1), T1;
let IdentityLogons = IdentityLogonEvents
| where Timestamp > ago(lookback)
| extend  hostname2 = extract("[^.]*",0, tolower(DeviceName)) 
| project D2 = tolower(hostname2), Application, IPAddress, OSPlatform, Timestamp;
OnbardedDevices | join IdentityLogons on $left.D1 == $right.D2
| summarize T2 = arg_max(Timestamp, *) by D2
| where T2 - T1 >= timeDiff
| summarize count() by Device = D1, LastSeenByMDI = T1, LastSeenByAAD = T2, Application, IPAddress, OSPlatform
| project-away count_
